buildscript {
    ext.springCloudContractVersion='2.0.2.RELEASE'
    
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springCloudContractVersion}"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id 'maven-publish'
}

apply plugin: 'spring-cloud-contract'

group = "com.example"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
    }
}

dependencies {
    implementation("com.sparkjava:spark-core:2.8.0")

    implementation("com.google.code.gson:gson:2.8.5")

    runtimeOnly("org.apache.logging.log4j:log4j-slf4j-impl:2.11.1")

    testImplementation "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
    testImplementation('org.springframework.cloud:spring-cloud-starter-contract-verifier') {
        exclude group: 'ch.qos.logback'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestGroovy.dependsOn(compileTestKotlin)
compileTestGroovy.classpath += files(compileTestKotlin.destinationDir)

contracts {
    packageWithBaseClasses = 'com.example.contracts'
    testMode = 'EXPLICIT'
    assertJsonSize = true // sic! https://cloud.spring.io/spring-cloud-static/spring-cloud-contract/2.0.2.RELEASE/multi/multi_contract-dsl.html#_limitations
}
