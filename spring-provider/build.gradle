buildscript {
    ext.springCloudContractVersion = '2.2.0.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springCloudContractVersion}"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id("org.jmailen.kotlinter") version "1.26.0"
    id 'maven-publish'
}

apply plugin: 'spring-cloud-contract'

group = "com.example"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.sparkjava:spark-core:2.9.1"

    implementation "com.google.code.gson:gson:2.8.6"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.13.0"

    testImplementation platform("org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}")
    testImplementation('org.springframework.cloud:spring-cloud-starter-contract-verifier') {
        exclude group: 'ch.qos.logback'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

contracts {
    packageWithBaseClasses = 'com.example.contracts'
    testMode = 'EXPLICIT'
    assertJsonSize = true // sic! https://cloud.spring.io/spring-cloud-static/spring-cloud-contract/2.2.0.RELEASE/reference/htmlsingle/#contract-limitations
}
